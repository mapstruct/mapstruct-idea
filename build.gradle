import com.hierynomus.gradle.license.tasks.LicenseCheck
import com.hierynomus.gradle.license.tasks.LicenseFormat

plugins {
    id "org.jetbrains.intellij" version "1.9.0"
    id "com.github.hierynomus.license" version "0.16.1"
    id "de.undercouch.download" version "4.1.2"
}


group pluginGroup
version pluginVersion

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'license'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenLocal()
    mavenCentral()
}

intellij {
    version = System.getenv().getOrDefault('IDEA_VERSION', ideaVersion)
    type = ideaType
    downloadSources = Boolean.valueOf(sources)
    sameSinceUntilBuild = Boolean.valueOf(isEAP)
    updateSinceUntilBuild = false
    pluginName = 'MapStruct-Intellij-Plugin'
    // The properties plugin is needed because Kotlin uses it
    // and for some reason plugins does not transitively pull itx
    plugins = ['java', 'Kotlin', 'properties', 'lombok']
}

// Simple function to load change-notes.html and description.html into valid text for plugin.xml
def htmlFixer = {f -> file(f).text.replace('<html>', '').replace('</html>', '')}

patchPluginXml {
    changeNotes = htmlFixer('change-notes.html')
    pluginDescription = htmlFixer('description.html')
}

task licenseTestData(type: LicenseCheck) {
    source = fileTree(dir: "testData").include("**/*")
}

task licenseFormatForKotlin(type: LicenseFormat) {
    source = fileTree(dir: "src/main").include("**/*.kt").include("**/*.xml")
}

license {
    header rootProject.file('etc/license.txt')
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE' // IntelliJ reports the JAVADOC_STYLE as a dangling comment
    }
    excludes([
            '**/META-INF/plugin.xml', // For some reason the plugin thinks that the license is not valid
            '**/*.properties',
            '**/inspectionDescriptions/*.html'
    ])
}

licenseFormat.dependsOn licenseFormatForKotlin
licenseTest.dependsOn licenseTestData

checkstyle {
    toolVersion '8.36.1'
    config resources.text.fromUri("https://raw.githubusercontent.com/mapstruct/mapstruct/master/build-config/src/main/resources/build-config/checkstyle.xml")
    configProperties = [
            'checkstyle.cache.file': rootProject.buildDir.toPath().resolve('checkstyle-cachefile').toString(),
            'basedir': 'https://raw.githubusercontent.com/mapstruct/mapstruct/master/build-config',
    ]
}

jacoco {
    toolVersion "0.8.8"
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

dependencies {
    implementation('org.mapstruct:mapstruct:1.5.3.Final')
    testImplementation(platform('org.junit:junit-bom:5.8.2'))
    testImplementation('org.junit.platform:junit-platform-launcher')
    testImplementation('org.junit.jupiter:junit-jupiter-api')
    testImplementation('org.junit.jupiter:junit-jupiter-engine')
    testRuntimeOnly('org.junit.vintage:junit-vintage-engine')
    testImplementation('org.assertj:assertj-core:3.11.1')
    testImplementation('org.apache.commons:commons-text:1.9')
}

task libs(type: Sync) {
    from configurations.runtimeClasspath
    into "$buildDir/libs"
    preserve {
        include 'mapstruct-intellij-*.jar'
    }
    rename 'mapstruct-1.5.3.Final.jar', 'mapstruct.jar'
}

def mockJdkLocation = "https://github.com/JetBrains/intellij-community/raw/master/java/mock"
def mockJdkDest = "$buildDir/mock"
def downloadMockJdk(mockJdkLocation, mockJdkDest, mockJdkVersion) {
    def location = mockJdkLocation + mockJdkVersion
    def destination = mockJdkDest + mockJdkVersion
    download {
        src([
            "$location/jre/lib/annotations.jar",
            "$location/jre/lib/rt.jar"
        ])
        dest "$destination/jre/lib/"
        overwrite false
        quiet false
    }
}

task downloadMockJdk7() {
    def jdkVersion = "JDK-1.7"
    def mockJdk7Location = mockJdkLocation + jdkVersion
    def mockJdk7Dest = mockJdkDest + jdkVersion
    downloadMockJdk(mockJdkLocation, mockJdkDest, jdkVersion)
    download {
        src([
             "$mockJdk7Location/src.zip"
        ])
        dest "$mockJdk7Dest"
        overwrite false
        quiet false
    }
}

task downloadMockJdk8() {
    downloadMockJdk(mockJdkLocation, mockJdkDest, "JDK-1.8")
}

task downloadMockJdk11() {
    downloadMockJdk(mockJdkLocation, mockJdkDest, "JDK-11")
}

test.dependsOn( libs, downloadMockJdk7, downloadMockJdk8, downloadMockJdk11 )
prepareTestingSandbox.dependsOn( libs )
prepareSandbox.dependsOn( libs )

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
}
