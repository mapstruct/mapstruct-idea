plugins {
    id "org.jetbrains.intellij" version "0.2.16"
    id "com.github.hierynomus.license" version "0.14.0"
    id "de.undercouch.download" version "3.2.0"
}


group pluginGroup
version pluginVersion

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'license'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'http://dl.bintray.com/jetbrains/intellij-plugin-service'
    }
}

intellij {
    version System.getenv().getOrDefault('IDEA_VERSION', ideaVersion)
    type ideaType
    downloadSources Boolean.valueOf(sources)
    sameSinceUntilBuild Boolean.valueOf(isEAP)
    alternativeIdePath idePath
    updateSinceUntilBuild false
    pluginName 'MapStruct-Intellij-Plugin'
}

// Simple function to load change-notes.html and description.html into valid text for plugin.xml
def htmlFixer = {f -> file(f).text.replace('<html>', '').replace('</html>', '')}

patchPluginXml {
    changeNotes = htmlFixer('change-notes.html')
    pluginDescription = htmlFixer('description.html')
}

task licenseTestData(type: com.hierynomus.gradle.license.tasks.LicenseCheck) {
    source = fileTree(dir: "testData").include("**/*")
}

license {
    header rootProject.file('etc/license.txt')
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE' // IntelliJ reports the JAVADOC_STYLE as a dangling comment
    }
    excludes([
            '**/META-INF/plugin.xml' // For some reason the plugin thinks that the license is not valid
    ])
}

licenseTest.dependsOn licenseTestData

checkstyle {
//    configFile file( new File(new URL("https://raw.githubusercontent.com/mapstruct/mapstruct/master/build-config/src/main/resources/build-config/checkstyle.xml").toURI( ) ) )
}

jacoco {
    toolVersion "0.7.9"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

dependencies {
    compile group: 'org.mapstruct', name: 'mapstruct-jdk8', version: '1.2.0.CR2'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.8.0'
}

task libs(type: Sync) {
    from configurations.compile
    into "$buildDir/libs"
    preserve {
        include 'mapstruct-intellij-*.jar'
    }
    rename 'mapstruct-jdk8.*', 'mapstruct.jar'
}

def mockJdkLocation = "https://github.com/JetBrains/intellij-community/raw/master/java/mockJDK-1.7"
def mockJdkDest = "$buildDir/mockJDK-1.7"
task downloadMockJdk() {
    download {
        src([
             "$mockJdkLocation/jre/lib/annotations.jar",
             "$mockJdkLocation/jre/lib/rt.jar"
        ])
        dest "$mockJdkDest/jre/lib/"
        overwrite false
        quiet false
    }
    download {
        src([
             "$mockJdkLocation/src.zip"
        ])
        dest "$mockJdkDest"
        overwrite false
        quiet false
    }
}

test.dependsOn( libs, downloadMockJdk )
